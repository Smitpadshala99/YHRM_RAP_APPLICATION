@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Employee CDS for managed with unmanaged'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.usageType:{
    serviceQuality: #X,
    sizeCategory: #S,
    dataClass: #MIXED
}

define view entity YHRM_MU_EMPLOYEE as select from yhrm_employee
association to parent YHRM_MU_JOB as _Job on $projection.JobId = _Job.JobId
composition[0..*] of YHRM_MU_TIMESHEET as _Timesheet
association[0..1] to YHRM_MU_ADDRESS as _Address on $projection.AddressId = _Address.AddressId
association[0..1] to YHRM_MU_DEPARTMENT as _Department on $projection.DepartmentId = _Department.DepartmentId
association[1] to YHRM_F4_DEPARTMENT as _DepartmentF4 on $projection.DepartmentId = _DepartmentF4.DepartmentId
association[1] to YHRM_F4_JOB as _JobF4 on $projection.JobId = _JobF4.JobId
association[0..*] to YHRM_F4_GENDER as _GenderF4 on $projection.Gender = _GenderF4.Value
association[0..1] to YHRM_MU_TIMESHEET_ATTENDANCE as _TimesheetAttendance on $projection.EmpId = _TimesheetAttendance.Empid
association[0..1] to YHRM_MU_TIMESHEET_COUNT as _TimesheetCount on $projection.EmpId = _TimesheetCount.Empid
{
    key emp_id as EmpId,
    first_name as FirstName,
    last_name as LastName,
    @Semantics.eMail.address: true
    email as Email,
    @Semantics.telephone.type: [#CELL]
    phone_no as PhoneNo,
    dob as Dob,
    @ObjectModel.text.element: ['_GenderF4.Description']
    gender as Gender,
    salary as Salary,
    hire_date as HireDate,
    active as Active,
    resign_date as ResignDate,
    address_id as AddressId,
    @ObjectModel.text.element: ['_JobF4.JobTitle']
    job_id as JobId,
    @ObjectModel.text.element: ['_DepartmentF4.DepartmentName']
    department_id as DepartmentId,
    supervisor_id as SupervisorId,
    @Semantics.user.createdBy: true
    created_by,
    @Semantics.systemDateTime.createdAt: true
    created_at,
    @Semantics.user.lastChangedBy: true
    last_changed_by,   
    @Semantics.user.localInstanceLastChangedBy: true  
    local_last_changed_by, 
    @Semantics.systemDateTime.localInstanceLastChangedAt: true
    local_last_changed_at as LocalLastChangedAt,
    @Semantics.systemDateTime.lastChangedAt: true
    last_changed_at as LastChangedAt,
    _Job,
    _DepartmentF4,
    _GenderF4,
    _JobF4,
    _Timesheet,
    _Address,
    _Department,
    _Address.StreetAdd1 as StreetAdd1,
    _Address.StreetAdd2 as StreetAdd2,
    _Address.StreetAdd3 as StreetAdd3,
    _Address.PinCode as PinCode,
    _Address.City as City,
    _Address.State as State,
    _Address.Country as Country,
    _TimesheetAttendance.AvailableCount as AvailableCount,
    _TimesheetCount.TotalTimesheet as TotalTimesheet,
    division(_TimesheetAttendance.AvailableCount, _TimesheetCount.TotalTimesheet, 2 ) * 100 as AttendancePercentage
}
