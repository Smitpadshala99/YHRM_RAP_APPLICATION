@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Connection view CDS Data Model'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.usageType:{
    serviceQuality: #X,
    sizeCategory: #S,
    dataClass: #MIXED
}
@UI.headerInfo: {
    typeName: 'Connection',
    typeNamePlural: 'Connections'
}
@Search.searchable: true
define view entity ZI_CONNECTION_SP
  as select from /dmo/connection as Connection
  association [1..*] to ZI_Flight_SP  as _Flight  on  $projection.CarrierId    = _Flight.CarrierId
                                                  and $projection.ConnectionId = _Flight.ConnectionId
  association [1]    to ZI_Carrier_SP as _Airline on  $projection.CarrierId = _Airline.CarrierId
{
      @UI.facet: [{ 
                    id: 'Connection',
                    purpose: #STANDARD,
                    type: #IDENTIFICATION_REFERENCE,
                    position: 10 ,
                    label: 'Connection Detail'},
                    { 
                    id: 'Flight',
                    purpose: #STANDARD,
                    type: #LINEITEM_REFERENCE,
                    position: 20 ,
                    label: 'Flights',
                    targetElement: '_Flight'
                    }
                    ]
      @UI.lineItem: [{ position: 10 }]
      @EndUserText.label: 'Airline'
      @UI.identification: [{ position: 10 }]
      @ObjectModel.text.association: '_Airline'
      @Search.defaultSearchElement: true
      @Search.fuzzinessThreshold: 0.8
  key carrier_id      as CarrierId,
      @UI.lineItem: [{ position: 20 }]
      @UI.identification: [{ position: 20 }]
      @Search.defaultSearchElement: true
  key connection_id   as ConnectionId,
      @UI.selectionField: [{ position: 10 }]
      @UI.lineItem: [{ position: 30 }]
      @UI.identification: [{ position: 30 }]
      @Search.defaultSearchElement: true
      @EndUserText.label: 'Departure Airport ID'
      @Consumption.valueHelpDefinition: [{ entity: { name: 'ZI_Airport_SP_F4' , element: 'AirportId' } }]
      airport_from_id as AirportFromId,
      @UI.selectionField: [{ position: 20 }]
      @UI.lineItem: [{ position: 40 }]
      @UI.identification: [{ position: 40 }]
      @Search.defaultSearchElement: true
      @EndUserText.label: 'Destination Airport ID'
      @Consumption.valueHelpDefinition: [{ entity: { name: 'ZI_Airport_SP_F4' , element: 'AirportId' } }]
      airport_to_id   as AirportToId,
      @UI.lineItem: [{ position: 50 }]
      @UI.identification: [{ position: 50 }]
      @EndUserText.label: 'Departure Time'
      @Search.defaultSearchElement: true
      departure_time  as DepartureTime,
      @UI.lineItem: [{ position: 60 }]
      @UI.identification: [{ position: 60 }]
      @EndUserText.label: 'Arrival Time'
      arrival_time    as ArrivalTime,
      @UI.lineItem: [{ position: 70 }]
      @UI.identification: [{ position: 70 }]
      @Semantics.quantity.unitOfMeasure: 'DistanceUnit'
      distance        as Distance,
      distance_unit   as DistanceUnit,
      @Search.defaultSearchElement: true
      _Flight,
      @Search.defaultSearchElement: true
      _Airline

}
